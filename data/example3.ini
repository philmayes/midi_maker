; This example illustrates triplets, timesig, and opus.
; Triplets are constructed thus:
;   A duration preceded by a "t" is 1/3 of that duration.
;   A duration preceded by a "d" is 2/3 of that duration.
;   An 8th note triplet is "tq,tq,tq", taking the same time as a quarter note.
;
; Note that you can use the same names in different types of definitions.
; For example, perc1 is both the name of a voice and a rhythm.

voice name=perc1 style=perc   voice=low_floor_tom
voice name=perc2 style=perc   voice=acoustic_bass_drum
voice name=perc3 style=perc   voice=open_hi_hat
voice name=john  style=rhythm voice=electric_guitar_distortion
voice name=jane  style=lead   voice=electric_bass_picked

; In rhythm commands, a minus sign before the duration represents a silence.
rhythm  name=guitar1 durations=-dq,tq,dq,tq,q,dq,tq
rhythm  name=guitar2 durations=-q,q,q,tq,tq,tq
rhythm  name=chorus  durations=q,q,tq,tq,tq
rhythm  name=perc1   durations=h,e,e,q
rhythm  name=perc2   durations=-s,q.,e,e,e.,e
rhythm  name=perc3   durations=-e,e,e,e,e,e,e,e
rhythm  name=coda    durations=tq,tq,tq,h.

tune    name=bass1  notes=dqE@3,tqE,dqG#,tqG#,dqB,tqB,dqC#@4,tqC#
tune    name=bass2  notes=dqE@4,tqE,dqC#,tqC#,dqB@3,tqB,dqG#,tqG#
tune    name=bass3  notes=dqE@4,tqE,dqD,tqD,dqC#,tqC#,dqB@3,tqB
tune    name=bass4  notes=B@4,A,G#,F#
tune    name=bass_chorus  notes=h.C#,h.F#,h.G#,h.C#

; An opus is a collection of compositions played in order.
; This allows you to construct a work from various compositions, e.g. intro,
; verse, chorus, verse, verse, coda. The current status (mute, volume, etc.) is
; carried forward from part to part. A composition need not even play anything;
; for example, the composition "setup" just mutes certain voices.
; You can name which opus or composition to play in the command line,
; e.g. "-n=blues". If no name is supplied, the first opus, if any,
; is chosen, else the first composition.
opus    name=blues compositions=setup,verse,chorus,verse,coda

composition name=chorus
tempo   bpm=120                     ; switch tempo and timesig so 1 bar
timesig value=3/4                   ; ...takes as long as 160:4/4
rhythm  voices=john rhythms=chorus  ; change the backing rhythm
effects voices=john staccato=0      ; cancel the staccato rhythm
play    voice=jane tunes=bass_chorus
bar     chords=C#m
bar     chords=F#m
bar     chords=G#
bar     chords=C#m
timesig value=4/4                   ; restore timesig for the following verses
unmute  voices=perc1,perc2,perc3    ; enable all percussion...
rhythm  voices=perc1 rhythms=perc1  ; ...and assign rhythms to them
rhythm  voices=perc2 rhythms=perc2
rhythm  voices=perc3 rhythms=perc3
volume  voices=perc1,perc2,perc3 level=110

composition name=setup
mute    voices=perc1,perc2,perc3

composition name=verse
tempo   bpm=160
; mute    voices=jane
rhythm  voices=john rhythms=guitar2; change the backing rhythm
effects voices=john staccato=200    ; set the staccato rhythm
play    voice=jane tunes=bass1,bass2,bass1,bass3
bar     chords=E repeat=3
bar     chords=E7
play    voice=jane tunes=bass1,bass2 transpose=5
effects voices=john staccato=0    ; set the staccato rhythm
bar     chords=A repeat=2
play    voice=jane tunes=bass1,bass2
effects voices=john staccato=200    ; set the staccato rhythm
bar     chords=E repeat=2
play    voice=jane tunes=bass1 transpose=7
effects voices=john staccato=0      ; cancel the staccato rhythm
bar     chords=B7
play    voice=jane tunes=bass1 transpose=5
bar     chords=A7
effects voices=john staccato=200    ; set the staccato rhythm
play    voice=jane tunes=bass1
bar     chords=E
play    voice=jane tunes=bass4
bar     chords=B7

composition name=coda
rhythm  voices=all rhythms=coda
bar     chords=E


