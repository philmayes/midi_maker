; Add some nuances to example1.mid:
; composition, tempo, mute, unmute, rhythm, volume, effects

; The voices and tune remain unchanged from example1:
voice name=bass    style=bass   voice=electric_bass_picked
voice name=rhythm1 style=rhythm voice=honky_tonk_piano
voice name=vocal   style=lead   voice=voice_oohs
tune name=tune1 notes=q,G,A,B,hC@6,h.C,qC,G@5,A,hB,h.B
tune name=tune2 notes=D,G,B,hA,h.A,eA,eA,qG,A,hB,h.B
tune name=tune3 notes=eG,eG,A,B,hC@6,h.C,eC,eC,qG@5,A,hB,n.B
tune name=tune4 notes=A,hA,A,q.F#,eD,qF#,A,nG

; A voice with style=bass will play the tonic of the chord each beat.
; The rhythm can be changed. Here different rhythms are described. Note that:
; * You can use full or abbreviated durations.
; * One has the same name as the voice. This is not necessary, just convenient.
;   The program will not confuse the two as they are used in different contexts.
rhythm name=bass  durations=half,quarter,q
rhythm name=bass2 durations=h,h
rhythm name=piano1 durations=q,e,e,q,q
rhythm name=piano2 durations=q,q,q,q ; straight 4/4 rhythm
rhythm name=coda  durations=note

composition name=one                 ; give this piece a name
tempo   bpm=180                      ; play it faster than the default 120 bpm
mute    voices=bass                  ; mute the bass (it's unmuted later)
bar     chords=G
play    voice=vocal tune=tune1,tune2,tune3
; A bar can have chord changes within it.
; The chords can be preceded by a duration; if not, a quarter note is assumed.
bar     chords=G,G,D,G7
; The rhythm commands above are used with a rhythm command in the performance.
; Yes, same name but different action! The rhythm command below plays the
; two rhythms "bass" and "bass2" in turn, each taking up one bar.
rhythm  voices=bass rhythms=bass,bass2
unmute  voices=bass                  ; unmute the bass
volume  voices=rhythm1 level=-5      ; make the rhythm quieter
bar     chords=C
bar     chords=h.C,D7                ; bar has 3 beats of Cmaj + 1 beat of D7.
bar     chords=G repeat=2            ; play 2 bars of G
rhythm  voices=rhythm1 rhythms=piano1; change the backing rhythm
bar     chords=D7
bar     chords=hD7,Am7,D7
bar     chords=G
bar     chords=hG,D,G7
rhythm  voices=rhythm1 rhythms=piano2; change the rhythm back to 4/4
effects voices=rhythm1 staccato=200  ; play the rhythm staccato
bar     chords=C
bar     chords=h.C,D7
bar     chords=G
bar     chords=G
effects voices=rhythm1 staccato=0    ; cancel the staccato rhythm
volume  voices=vocal level=127 rate=3; Make the lead volume increase
play    voice=vocal tune=tune4
bar     chords=D7
bar     chords=D7
bar     chords=G
rhythm  voices=rhythm1,bass rhythms=coda
bar     chords=G
